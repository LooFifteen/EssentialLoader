repositories {
    maven { url "https://maven.minecraftforge.net/" }
    maven { url "https://repo.spongepowered.org/maven" }
}

sourceSets {
    exampleMod
    example2Mod
    essential
    dummyStage1
    dummyStage2
    dummyStage3
}

configurations {
    example2ModImplementation.extendsFrom(exampleModImplementation)
    example2ModCompileOnly.extendsFrom(exampleModCompileOnly)

    mixin07
    mixin08
}

dependencies {
    def launchwrapper = "net.minecraft:launchwrapper:1.12"
    // Intentionally using a different version than stage1 to make sure it can determine the version correctly
    def forge = "net.minecraftforge:forge:1.8.8-11.15.0.1655:universal"

    implementation("org.junit.jupiter:junit-jupiter:5.7.2")

    // Versions based on the one which MC include by default in 1.8.9 (minimal supported version)
    // See https://github.com/MultiMC/meta-multimc/blob/master/net.minecraft/1.8.9.json
    implementation(launchwrapper)
    runtimeOnly("org.apache.commons:commons-lang3:3.3.2")
    runtimeOnly("commons-io:commons-io:2.4")
    runtimeOnly("commons-codec:commons-codec:1.9")
    runtimeOnly("org.apache.logging.log4j:log4j-api:2.0-beta9")
    implementation("com.google.code.gson:gson:2.2.4")
    implementation("com.google.guava:guava:17.0")
    runtimeOnly("com.mojang:authlib:1.5.21")
    // Forge
    implementation(forge)
    runtimeOnly("lzma:lzma:0.0.1")
    // Mixin 0.8
    runtimeOnly("org.ow2.asm:asm-commons:6.2")
    runtimeOnly("com.google.guava:guava:21.0")

    exampleModImplementation(project(":stage0:launchwrapper"))
    exampleModCompileOnly(launchwrapper)
    exampleModCompileOnly(forge)
    exampleModCompileOnly("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }

    mixin07("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        transitive = false
    }
    mixin08("org.spongepowered:mixin:0.8.2") {
        transitive = false
    }

    essentialCompileOnly(launchwrapper)
    essentialCompileOnly(forge)

    dummyStage1CompileOnly(launchwrapper)
    dummyStage2CompileOnly(launchwrapper)
    dummyStage3CompileOnly(launchwrapper)
}

def integrationTest = tasks.register("integrationTest", Test) {
    testClassesDirs = sourceSets.main.output.classesDirs
    classpath = sourceSets.main.runtimeClasspath

    useJUnitPlatform()

    inputs.dir(tasks.setupDownloadsApi.destinationDir)
    dependsOn(tasks.setupDownloadsApi)
}
check.dependsOn(integrationTest)

def includeMixin(AbstractArchiveTask task, Configuration mixin) {
    task.dependsOn(mixin)
    task.from({ mixin.collect { zipTree(it) } }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}

def configureExampleModJar = { String tweaker, Configuration mixin = null -> return { AbstractArchiveTask task ->
    archiveBaseName.set(name)
    from(sourceSets.exampleMod.output)
    dependsOn(configurations.exampleModRuntimeClasspath)
    from({ configurations.exampleModRuntimeClasspath.collect { zipTree(it) } })

    manifest {
        attributes "FMLCorePlugin": "com.example.mod.ExampleCoreMod",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "TweakClass": tweaker,
                "TweakOrder": "0"
    }

    if (mixin != null) {
        includeMixin(task, mixin)
        manifest.attributes "MixinConfigs": "examplemod.mixins.json"
    }
} }

def configureExample2ModJar = { String tweaker, Configuration mixin = null -> return { AbstractArchiveTask task ->
    archiveBaseName.set(name)
    from(sourceSets.example2Mod.output)
    dependsOn(configurations.example2ModRuntimeClasspath)
    from({ configurations.example2ModRuntimeClasspath.collect { zipTree(it) } })

    manifest {
        attributes "FMLCorePlugin": "com.example.mod2.ExampleCoreMod",
                "FMLCorePluginContainsFMLMod": "Yes, yes it does",
                "TweakClass": tweaker,
                "TweakOrder": "0"
    }

    if (mixin != null) {
        includeMixin(task, mixin)
        manifest.attributes "MixinConfigs": "example2mod.mixins.json"
    }
} }

tasks.register("exampleModJar", Jar, configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker"))
tasks.register("example2ModJar", Jar, configureExample2ModJar("com.example.mod2.tweaker.ExampleModTweaker"))
tasks.register("exampleModEssentialTweakerJar", Jar, configureExampleModJar("gg.essential.loader.stage0.EssentialSetupTweaker"))
tasks.register("example2ModEssentialTweakerJar", Jar, configureExample2ModJar("gg.essential.loader.stage0.EssentialSetupTweaker"))

tasks.register("exampleModWithMixin07Jar", Jar, configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker", configurations.mixin07))
tasks.register("example2ModWithMixin07Jar", Jar, configureExample2ModJar("com.example.mod2.tweaker.ExampleModTweaker", configurations.mixin07))
tasks.register("exampleModEssentialTweakerWithMixin07Jar", Jar, configureExampleModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin07))
tasks.register("example2ModEssentialTweakerWithMixin07Jar", Jar, configureExample2ModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin07))

tasks.register("exampleModWithMixin08Jar", Jar, configureExampleModJar("com.example.mod.tweaker.ExampleModTweaker", configurations.mixin08))
tasks.register("example2ModWithMixin08Jar", Jar, configureExample2ModJar("com.example.mod2.tweaker.ExampleModTweaker", configurations.mixin08))
tasks.register("exampleModEssentialTweakerWithMixin08Jar", Jar, configureExampleModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin08))
tasks.register("example2ModEssentialTweakerWithMixin08Jar", Jar, configureExample2ModJar("gg.essential.loader.stage0.EssentialSetupTweaker", configurations.mixin08))

tasks.register("essentialJar", Jar) {
    archiveBaseName.set("essential")
    from(sourceSets.essential.output)
    includeMixin(it, configurations.mixin07)
}

tasks.register("dummyStage1Jar", Jar) {
    archiveBaseName.set("dummyStage1")
    from(sourceSets.dummyStage1.output)
}

tasks.register("dummyStage2Jar", Jar) {
    archiveBaseName.set("dummyStage2")
    from(sourceSets.dummyStage2.output)
}

tasks.register("dummyStage3Jar", Jar) {
    archiveBaseName.set("dummyStage3")
    from(sourceSets.dummyStage3.output)
    includeMixin(it, configurations.mixin07)
}

tasks.register("setupDownloadsApi", Sync) {
    def downloadsApi = new File(project.buildDir, "downloadsApi")
    into(downloadsApi)

    def mod = { Provider<RegularFile> source, String dst ->
        def dstJar = "${dst}.jar"
        preserve.include(dst)
        preserve.include(dstJar)
        from(source) {
            rename { dstJar }
        }
        doLast {
            def file = new File(downloadsApi, dstJar).absoluteFile
            new File(downloadsApi, dst).write("""{ "url": "${file.toURI()}", "checksum": "${file.bytes.md5()}" }""")
        }
    }
    def platformForge = "forge_1-8-8"

    mod(tasks.exampleModJar.archiveFile, "v1/mods/example/mod/updates/stable/$platformForge")
    mod(tasks.example2ModJar.archiveFile, "v1/mods/example/mod2/updates/stable/$platformForge")
    mod(tasks.exampleModEssentialTweakerJar.archiveFile, "v1/mods/example/mod/updates/essential-tweaker/$platformForge")
    mod(tasks.example2ModEssentialTweakerJar.archiveFile, "v1/mods/example/mod2/updates/essential-tweaker/$platformForge")

    mod(tasks.exampleModWithMixin07Jar.archiveFile, "v1/mods/example/mod/updates/stable-with-mixin-07/$platformForge")
    mod(tasks.example2ModWithMixin07Jar.archiveFile, "v1/mods/example/mod2/updates/stable-with-mixin-07/$platformForge")
    mod(tasks.exampleModEssentialTweakerWithMixin07Jar.archiveFile, "v1/mods/example/mod/updates/essential-tweaker-with-mixin-07/$platformForge")
    mod(tasks.example2ModEssentialTweakerWithMixin07Jar.archiveFile, "v1/mods/example/mod2/updates/essential-tweaker-with-mixin-07/$platformForge")

    mod(tasks.exampleModWithMixin08Jar.archiveFile, "v1/mods/example/mod/updates/stable-with-mixin-08/$platformForge")
    mod(tasks.example2ModWithMixin08Jar.archiveFile, "v1/mods/example/mod2/updates/stable-with-mixin-08/$platformForge")
    mod(tasks.exampleModEssentialTweakerWithMixin08Jar.archiveFile, "v1/mods/example/mod/updates/essential-tweaker-with-mixin-08/$platformForge")
    mod(tasks.example2ModEssentialTweakerWithMixin08Jar.archiveFile, "v1/mods/example/mod2/updates/essential-tweaker-with-mixin-08/$platformForge")

    mod(evaluationDependsOn(':stage2:launchwrapper').tasks.jar.archiveFile, "v1/mods/essential/loader-stage2/updates/stable/$platformForge")
    mod(tasks.essentialJar.archiveFile, "v1/mods/essential/essential/updates/stable/$platformForge")
    mod(tasks.dummyStage1Jar.archiveFile, "v1/mods/essential/loader-stage1/updates/dummy/$platformForge")
    mod(tasks.dummyStage2Jar.archiveFile, "v1/mods/essential/loader-stage2/updates/dummy/$platformForge")
    mod(tasks.dummyStage3Jar.archiveFile, "v1/mods/essential/essential/updates/dummy/$platformForge")
}
